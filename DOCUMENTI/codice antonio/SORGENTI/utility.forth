
DECIMAL

0 CONSTANT IN
1 CONSTANT OUT

\ PAROLA CHE RITORNA IL VALORE CONTENUTO NEL REGISTRO TIME
\ ( -- TIME )
: TIMESTAMP TIMER @ ; 

\ RITARDO DIPENDENTE DAL TEMPO
\ ( TEMPO_MICROSEC -- )
: DELAY TIMESTAMP BEGIN 2DUP TIMESTAMP SWAP - <= UNTIL 2DROP ;


\ SELEZIONA IL REGISTRO GPIOFSEL
\ (GPIO -- GPIO GPIOFSEL)
: SET_GPIOFSEL DUP 10 / 4 * GPIOFSEL + ;

\ PAROLA CHE IMPOSTA IL VALORE LOGICO DEI PIN 
\ IN INPUT (000), IN OUTPUT (001) O IN ALT5 (010)
\ (GPIO VALORE_LOGICO -- )
: ABIL_PIN >R SET_GPIOFSEL SWAP 10 MOD 3 * 
			2DUP 7 SWAP LSHIFT INVERT OVER @ AND SWAP ! 
			R> SWAP LSHIFT OVER @ OR SWAP ! ;
			
\ PAROLE CHE IMPOSTANO IL VALORE LOGICO NEI REGISTRI GPIOSET E GPIOCLR
\ (LOGVAL GPIO -- )
: GPIOSET_LOG LSHIFT GPIOSET ! ;
: GPIOCLR_LOG LSHIFT GPIOCLR ! ;

\ PAROLA CHE DETERMINA IL VALORE LOGICO DEL GPIO PASSATO IN INPUT
\ (GPIO -- LOGVAL)
: READLEV 1 SWAP LSHIFT GPIOLEV @ AND 0 = IF 0 ELSE 1 THEN ;


\ PAROLE CHE INCREMENTANO O DECREMENTANO IL VALORE DI UN CONTATORE
\ (VALORE VARIABILE -- )
: --I DUP @ ROT - SWAP ! ;
: ++I DUP @ ROT + SWAP ! ;

