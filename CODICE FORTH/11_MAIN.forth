
DECIMAL


VARIABLE ROOM \ STANZA SELEZIONATA 


VARIABLE II \ CONTATORE CICLO INTERNO


40 CONSTANT MIN_H \ VALORE MINIMO DI UMIDITA'

69 CONSTANT MAX_H \ VALORE MASSIMO DI UMIDITA'

10 CONSTANT MIN_T \ VALORE MINIMO DI TEMPERATURA

27 CONSTANT MAX_T \ VALORE MASSIMO DI TEMPERATURA


-2 CONSTANT FLAGDIGIT \ VARIABILE DI UTILITA' PER TERMINARE L'INVIO DI DATI


CREATE HUMIDITY NROOMS 1 - ALLOT ALIGN \ ARRAY DOVE VENGONO SALVATI I VALORI DELLE ULTIME MISURAZIONI DELL'UMIDITA PER OGNI STANZA
CREATE TEMPERATURE NROOMS 1 - ALLOT ALIGN \ ARRAY DOVE VENGONO SALVATI I VALORI DELLE ULTIME MISURAZIONI DELLA TEMPERATURA PER OGNI STANZA

\ PAROLA PER SALVARE UMIDITA E TEMPERATURA LETTE NEGLI ARRAY
\( STANZA(II) --- )
: MSTORE CHECKSUM IF DUP HHUMIDITY @ HUMIDITY ROT ASTORE HTEMPERATURE @ TEMPERATURE ROT ASTORE ELSE DROP THEN ;

\ PAROLA CHE DATO UN NUMERO LO DIVIDE NELLE SUE CIFRE E LE INVIA DALLA PIU SIGNIFICATIVA ALLA MENO SIGNIFICATIVA AL DISPLAY LCD
\(MISURA ---)
: PRINTNUMBER FLAGDIGIT SWAP BEGIN LASTDIGIT DUP 9 > WHILE REPEAT 
                             BEGIN DUP FLAGDIGIT <> WHILE DIGITPRINT REPEAT DROP ;

\ PAROLA CHE PERMETTE DI VISAULIZZARE NEL DISPLAY LCD LA SEQUENZA  " S:<<NUMERO STANZA>> "
\(---) 
: ROOMDISPLAY STANZA CHARSEND PUNTI CHARSEND ROOM @ PRINTNUMBER SPACE CHARSEND  ;

\PAROLA DI UTILITA CHE RECUPERA LA TEMPERATURA DELLA STANZA CORRENTE
\(--- TEMPERATURA)
: TEMPFETCH TEMPERATURE II @ AREAD ;

\ PAROLA CHE PERMETTE DI VISAULIZZARE NEL DISPLAY LCD LA SEQUENZA  " T:<<TEMPERATURA STANZA>> "
\(---)
: TEMPDISPLAY GRADI CHARSEND PUNTI CHARSEND TEMPERATURE ROOM @ AREAD PRINTNUMBER SPACE CHARSEND  ;

\PAROLA DI UTILITA CHE RECUPERA L'UMIDITA DELLA STANZA CORRENTE
\(--- UMIDITA)
: HUMFETCH HUMIDITY II @ AREAD ;

\ PAROLA CHE PERMETTE DI VISAULIZZARE NEL DISPLAY LCD LA SEQUENZA  " H:<<UMIDITA STANZA>> "
\(---)
: HUMDISPLAY HUM CHARSEND PUNTI CHARSEND HUMIDITY ROOM @ AREAD PRINTNUMBER ;

\ COMPOSTA DA 
\(---)
: DISPLAY ROOMDISPLAY TEMPDISPLAY  HUMDISPLAY ;

\PAROLA CHE RIPULISCE LO SCHERMO E VISUALIZZA I NUOVI DATI NEL DISPLAY LCD
\(---)
: SHOW DISPLAYCLEAR 10000 DELAY DISPLAY ;

\PAROLA PER VERIFICARE CHE TEMPERATURA E UMIDITA' RIENTRANO NEI VALORI DI SOGLIA
\(--- BOOL)
: CHECK HUMFETCH  DUP MIN_H > SWAP MAX_H < AND TEMPFETCH  DUP MIN_T > SWAP MAX_T < AND AND ; 

\PAROLA CHE MODIFICA IL VALORE DI ROOM ( LA STANZA DI CUI VERRANO MOSTRATE LE INFORMAZIONI SUL DISPLAY LCD) 
\ ( -- )
: CHANGEROOM ROOM @ 1 + NROOMS MOD ROOM ! ;

\ PAROLA CHE INIZIALIZZA LE VARIABILI DEL SISTEMA E IL DISPLAY LCD
\( -- )
: RESET INITC LCDINIT BEGIN COUNTER @ WHILE 1 COUNTER DECC 
                                            COUNTER @ WHITE SETCOLOR 
                                            COUNTER @ OFF RING 
                                            COUNTER @ APRI FIREDOOR  
                      REPEAT -1 ROOM ! 0 II ! ; 
                              
                                            
\(---) 
: MAIN RESET BEGIN  
              0 II !  BEGIN
                        II @ NROOMS < WHILE  
                            II @ DUP RILEVAZIONE MSTORE CHECK IF 
                                                                II @ GREEN SETCOLOR
                                                              ELSE
                                                                II @ YELLOW SETCOLOR 
                                                              THEN  
                                                  II @ ISFIRE IF
                                                                II @ DUP DUP RED SETCOLOR ON RING CHIUDI FIREDOOR
                                                              ELSE
                                                                II @ DUP OFF RING APRI FIREDOOR
                                                              THEN
                                                                1 II ADDC 1500000 DELAY                                                                                                 
                      REPEAT 
                CHANGEROOM SHOW            
              AGAIN ;



